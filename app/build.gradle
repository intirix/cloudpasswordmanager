apply plugin: 'com.android.application'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


android {
    compileSdkVersion 24
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.intirix.cloudpasswordmanager"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 13
        versionName "1.5.0"
    }
    signingConfigs {
        myrelease {
            storeFile file(System.getProperty("user.home")+"/.keys/keystore")
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
            keyAlias "intirix"
            keyPassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
        }
    }
    buildTypes {
        debug {
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date("+System.currentTimeMillis()+"L)"
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
            buildConfigField "boolean", "ALLOW_SCREENSHOTS", "true"
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.myrelease
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date("+System.currentTimeMillis()+"L)"
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
            buildConfigField "boolean", "ALLOW_SCREENSHOTS", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    compile project(':secretsmanagerapiv1')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    annotationProcessor 'com.squareup:javapoet:1.7.0'
    annotationProcessor "com.google.dagger:dagger-compiler:2.11"


    testCompile 'org.robolectric:robolectric:3.1'
    testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1'

    testCompile 'org.easymock:easymock:3.1'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'


    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.jakewharton:butterknife:8.8.1'
    compile 'com.google.dagger:dagger:2.11'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.android.support:design:24.2.0'

    compile 'org.greenrobot:eventbus:3.0.0'
    //compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.57'
    compile group: 'com.madgag.spongycastle', name: 'prov', version: '1.56.0.0'
}