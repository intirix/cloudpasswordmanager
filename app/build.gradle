apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.intirix.cloudpasswordmanager"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
    }
    signingConfigs {
        myrelease {
            storeFile file(System.getProperty("user.home")+"/.keys/keystore")
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
            keyAlias "intirix"
            keyPassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
        }
    }
    buildTypes {
        debug {
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date("+System.currentTimeMillis()+"L)"
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.myrelease
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date("+System.currentTimeMillis()+"L)"
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    apt 'com.squareup:javapoet:1.7.0'
    apt 'com.google.dagger:dagger-compiler:2.2'


    testCompile "org.robolectric:robolectric:3.1"
    testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1'

    testCompile 'org.easymock:easymock:3.1'


    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.jakewharton:butterknife:8.1.0'
    compile 'com.google.dagger:dagger:2.2'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.android.support:design:23.4.0'

    compile 'org.greenrobot:eventbus:3.0.0'
}
